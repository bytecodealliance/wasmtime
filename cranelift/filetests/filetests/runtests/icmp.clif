test interpret
test run
target aarch64
target s390x
target x86_64
target riscv64
target riscv64 has_c has_zcb
target pulley32
target pulley32be
target pulley64
target pulley64be

; This test is also a regression test for aarch64.
; We were not correctly handling the fact that the rhs constant value
; overflows its type when viewed as a signed value, and thus encoding the wrong
; value into the resulting instruction.
function %overflow_rhs_const(i8) -> i8 {
block0(v0: i8):
    v1 = iconst.i8 192
    v2 = icmp sge v0, v1
    return v2
}
; run: %overflow_rhs_const(49) == 1
; run: %overflow_rhs_const(-65) == 0

function %ugt_sub_swap(i32, i32) -> i8 fast {
block0(v0: i32, v1: i32):
    v2 = isub v0, v1
    v3 = icmp ugt v2, v0
    return v3
}

; run: %ugt_sub_swap(1, -49) == 1
; run: %ugt_sub_swap(1, 2) == 1
; run: %ugt_sub_swap(2, 1) == 0

function %ule_sub_swap(i32, i32) -> i8 fast {
block0(v0: i32, v1: i32):
    v2 = isub v0, v1
    v3 = icmp ule v2, v0
    return v3
}

; run: %ule_sub_swap(1, -88) == 0
; run: %ule_sub_swap(1, 2) == 0
; run: %ule_sub_swap(2, 1) == 1

function %fold_cmp_over_nots1(i8, i8) -> i8 {
block0(v0: i8, v2: i8):
    v1 = bnot v0
    v3 = bnot v2
    v4 = icmp ult v1, v3
    return v4
}

; run: %fold_cmp_over_nots1(2, 1) == 1

function %fold_cmp_over_nots2(i8, i8) -> i8 {
block0(v0: i8, v2: i8):
    v1 = bnot v0
    v3 = bnot v2
    v4 = icmp slt v1, v3
    return v4
}

; run: %fold_cmp_over_nots2(2, 1) == 1

;; (bxor ty (slt cty x y) (sgt cty x y)) -> (ne cty x y)
function %test_bxor_slt_sgt(i32, i32) -> i8 fast {
block0(v0: i32, v1: i32):
    v2 = icmp slt v0, v1
    v3 = icmp sgt v0, v1
    v4 = bxor v2, v3
    return v4
}

; run: %test_bxor_slt_sgt(1, 1) == 0
; run: %test_bxor_slt_sgt(1, 3) == 1

;; (bxor ty (ult cty x y) (ugt cty x y)) -> (ne cty x y)
function %test_bxor_ult_ugt(i32, i32) -> i8 fast {
block0(v0: i32, v1: i32):
    v2 = icmp ult v0, v1
    v3 = icmp ugt v0, v1
    v4 = bxor v2, v3
    return v4
}

; run: %test_bxor_ult_ugt(1, 1) == 0
; run: %test_bxor_ult_ugt(1, 3) == 1

;; Rule 1: (x <_s y) & (x >_s y) => false
function %test_slt_sgt(i32, i32) -> i8 fast {
block0(v0: i32, v1: i32):
    v2 = icmp slt v0, v1  ; (x <_s y)
    v3 = icmp sgt v0, v1  ; (x >_s y)
    v4 = band v2, v3       ; (x <_s y) & (x >_s y)
    return v4
}

; run: %test_slt_sgt(42, 24) == 0

;; Rule 2: (x <_u y) & (x >_u y) => false
function %test_ult_ugt(i32, i32) -> i8 fast {
block0(v0: i32, v1: i32):
    v2 = icmp ult v0, v1  ; (x <_u y)
    v3 = icmp ugt v0, v1  ; (x >_u y)
    v4 = band v2, v3       ; (x <_u y) & (x >_u y)
    return v4
}

; run: %test_ult_ugt(42, 24) == 0

;; Rule 3: (x <_s y_const) & (x >_u y_const) => false, if y_const < 0
function %test_slt_ugt_neg_const_y(i32) -> i8 fast {
block0(v0: i32):
    v1 = iconst.i32 -10      ; y_const = -10. Triggers (i64_lt y 0)
    v2 = icmp slt v0, v1  ; (x <_s -10)
    v3 = icmp ugt v0, v1  ; (x >_u -10)
    v4 = band v2, v3
    return v4
}

; run: %test_slt_ugt_neg_const_y(42) == 0

;; Rule 4: (x_const <_s y) & (x_const >_u y) => false, if x_const >= 0
function %test_slt_ugt_pos_const_x(i32) -> i8 fast {
block0(v0: i32):
    v1 = iconst.i32 10
    v2 = icmp slt v1, v0
    v3 = icmp ugt v1, v0
    v4 = band v2, v3
    return v4
}

; run: %test_slt_ugt_pos_const_x(42) == 0

;; Rule 5: (x <_u y_const) & (x >_s y_const) => false, if y_const >= 0
function %test_ult_sgt_pos_const_y(i32) -> i8 fast {
block0(v0: i32):
    v1 = iconst.i32 10
    v2 = icmp ult v0, v1
    v3 = icmp sgt v0, v1
    v4 = band v2, v3
    return v4
}

; run: %test_ult_sgt_pos_const_y(42) == 0

;; Rule 6: (x_const <_u y) & (x_const >_s y) => false, if x_const < 0
function %test_ult_sgt_neg_const_x(i32) -> i8 fast {
block0(v0: i32):
    v1 = iconst.i32 -10
    v2 = icmp ult v1, v0
    v3 = icmp sgt v1, v0
    v4 = band v2, v3
    return v4
}

; run: %test_ult_sgt_neg_const_x(42) == 0

;; (x <u y) & (x != -1) => (x <_u y)
function %test_ult_ne_minus_one(i32, i32) -> i8 {
block0(v0: i32, v1: i32):
    v2 = icmp ult v0, v1
    v3 = iconst.i32 -1
    v4 = icmp ne v0, v3
    v5 = band v2, v4
    return v5
}
; run: %test_ult_ne_minus_one(3, 5) == 1
; run: %test_ult_ne_minus_one(-1, 1) == 0
