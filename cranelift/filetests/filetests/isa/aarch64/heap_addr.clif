test compile precise-output
set unwind_info=false
set enable_heap_access_spectre_mitigation=true
target aarch64

function %dynamic_heap_check(i64 vmctx, i32) -> i64 {
    gv0 = vmctx
    gv1 = load.i64 notrap aligned gv0
    heap0 = dynamic gv0, bound gv1, offset_guard 0x1000, index_type i32

block0(v0: i64, v1: i32):
    v2 = heap_addr.i64 heap0, v1, 0
    return v2
}

; block0:
;   mov w10, w1
;   ldr x5, [x0]
;   mov x11, x5
;   subs xzr, x10, x11
;   b.ls label1 ; b label2
; block1:
;   add x13, x0, x1, UXTW
;   subs xzr, x10, x11
;   movz x14, #0
;   csel x0, x14, x13, hi
;   ret
; block2:
;   udf #0xc11f

function %static_heap_check(i64 vmctx, i32) -> i64 {
    gv0 = vmctx
    heap0 = static gv0, bound 0x1_0000, offset_guard 0x1000, index_type i32

block0(v0: i64, v1: i32):
    v2 = heap_addr.i64 heap0, v1, 0
    return v2
}

; block0:
;   mov w8, w1
;   subs xzr, x8, #65536
;   b.ls label1 ; b label2
; block1:
;   add x10, x0, x1, UXTW
;   subs xzr, x8, #65536
;   movz x11, #0
;   csel x0, x11, x10, hi
;   ret
; block2:
;   udf #0xc11f

