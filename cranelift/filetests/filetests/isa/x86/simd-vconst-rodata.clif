test rodata
set enable_simd=true
target x86_64 haswell

function %vconst_i32() -> i32x4 {
block0:
    v0 = vconst.i32x4 0x1234
    return v0
}

; sameln: [34, 12, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]

function %vconst_b16() -> b16x8 {
block0:
    v0 = vconst.b16x8 [true false true false true false true true]
    return v0
}

; sameln: [1, 0, 0, 0, 1, 0, 0, 0, 1, 0, 0, 0, 1, 0, 1, 0]


; Since both jump tables and constants are emitted after the function body, it is important that they do not interfere.
; This test shows that even in the presence of jump tables, constants are emitted correctly
function %vconst_with_jumptables() {
jt0 = jump_table [block0]

block10:
    v10 = iconst.i64 0
    br_table v10, block1, jt0
block0:
    jump block11
block1:
    jump block11
block11:
    v11 = vconst.i8x16 [1 2 3 4 5 6 7 8 9 10 11 12 13 14 15 16]
    return
}

; sameln: [1, 2, 3, 4, 5, 6, 7, 8, 9, A, B, C, D, E, F, 10]

function %vconst_preamble() -> b16x8 {
const42 = i32x4 [0 1 2 3]
const43 = i32x4 [4 5 6 7]
block0:
    v0 = vconst.b16x8 const42
    return v0
}

; sameln: [0, 0, 0, 0, 1, 0, 0, 0, 2, 0, 0, 0, 3, 0, 0, 0, 4, 0, 0, 0, 5, 0, 0, 0, 6, 0, 0, 0, 7, 0, 0, 0]
