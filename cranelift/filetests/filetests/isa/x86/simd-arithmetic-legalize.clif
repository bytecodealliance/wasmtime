test legalizer
set enable_simd
target x86_64 skylake

function %ineg_i32x4() -> b1 {
ebb0:
    v0 = vconst.i32x4 [1 1 1 1]
    v2 = ineg v0
    ; check: v5 = vconst.i32x4 0x00
    ; nextln: v2 = isub v5, v0

    v3 = extractlane v2, 0
    v4 = icmp_imm eq v3, -1

    return v4
}

function %ineg_legalized() {
ebb0:
    v0 = vconst.i8x16 0x00
    v1 = ineg v0
    ; check: v6 = vconst.i8x16 0x00
    ; nextln: v1 = isub v6, v0

    v2 = raw_bitcast.i16x8 v0
    v3 = ineg v2
    ; check: v7 = vconst.i16x8 0x00
    ; nextln: v3 = isub v7, v2

    v4 = raw_bitcast.i64x2 v0
    v5 = ineg v4
    ; check: v8 = vconst.i64x2 0x00
    ; nextln: v5 = isub v8, v4

    return
}

function %fneg_legalized() {
ebb0:
    v0 = vconst.f32x4 [0x1.0 0x2.0 0x3.0 0x4.0]
    v1 = fneg v0
    ; check: v4 = vconst.f32x4 0x00
    ; nextln: v1 = fsub v4, v0

    v2 = vconst.f64x2 [0x1.0 0x2.0]
    v3 = fneg v2
    ; check: v5 = vconst.f64x2 0x00
    ; nextln: v3 = fsub v5, v2

    return
}

function %fabs_legalized() {
ebb0:
    v0 = vconst.f64x2 [0x1.0 -0x2.0]
    v1 = fabs v0
    ; check: v2 = vconst.i64x2 0xffffffffffffffffffffffffffffffff
    ; nextln: v3 = ushr_imm v2, 1
    ; nextln: v4 = raw_bitcast.f64x2 v3
    ; nextln: v1 = band v0, v4
    return
}
