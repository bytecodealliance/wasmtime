test compile
target s390x

function %dynamic_heap_check(i64 vmctx, i32) -> i64 {
    gv0 = vmctx
    gv1 = load.i64 notrap aligned gv0
    heap0 = dynamic gv0, bound gv1, offset_guard 0x1000, index_type i32

block0(v0: i64, v1: i32):
    v2 = heap_addr.i64 heap0, v1, 0
    return v2
}

; check: Block 0:
; check:  llgfr %r3, %r3
; nextln: lg %r4, 0(%r2)
; nextln: aghi %r4, 0
; nextln: clgr %r3, %r4
; nextln: jgnh label1 ; jg label2
; check: Block 1:
; check: agr %r2, %r3
; nextln: lghi %r5, 0
; nextln: clgr %r3, %r4
; nextln: locgrh %r2, %r5
; nextln: br %r14
; check: Block 2:
; check:  trap

function %static_heap_check(i64 vmctx, i32) -> i64 {
    gv0 = vmctx
    heap0 = static gv0, bound 0x1_0000, offset_guard 0x1000, index_type i32

block0(v0: i64, v1: i32):
    v2 = heap_addr.i64 heap0, v1, 0
    return v2
}

; check: Block 0:
; check:  llgfr %r3, %r3
; nextln: clgfi %r3, 65536
; nextln: jgnh label1 ; jg label2
; check: Block 1:
; check: agr %r2, %r3
; nextln: lghi %r4, 0
; nextln: clgfi %r3, 65536
; nextln: locgrh %r2, %r4
; nextln: br %r14
; check: Block 2:
; check:  trap
