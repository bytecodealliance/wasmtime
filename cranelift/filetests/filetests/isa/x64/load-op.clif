test compile precise-output
target x86_64

function %add_from_mem_u32_1(i64, i32) -> i32 {
block0(v0: i64, v1: i32):
  v2 = load.i32 v0
  v3 = iadd.i32 v2, v1
  return v3
}

; block0:
;   movq    %rsi, %rax
;   addl    %eax, 0(%rdi), %eax
;   ret

function %add_from_mem_u32_2(i64, i32) -> i32 {
block0(v0: i64, v1: i32):
  v2 = load.i32 v0
  v3 = iadd.i32 v1, v2
  return v3
}

; block0:
;   movq    %rsi, %rax
;   addl    %eax, 0(%rdi), %eax
;   ret

function %add_from_mem_u64_1(i64, i64) -> i64 {
block0(v0: i64, v1: i64):
  v2 = load.i64 v0
  v3 = iadd.i64 v2, v1
  return v3
}

; block0:
;   movq    %rsi, %rax
;   addq    %rax, 0(%rdi), %rax
;   ret

function %add_from_mem_u64_2(i64, i64) -> i64 {
block0(v0: i64, v1: i64):
  v2 = load.i64 v0
  v3 = iadd.i64 v1, v2
  return v3
}

; block0:
;   movq    %rsi, %rax
;   addq    %rax, 0(%rdi), %rax
;   ret

function %add_from_mem_not_narrow(i64, i8) -> i8 {
block0(v0: i64, v1: i8):
  v2 = load.i8 v0
  v3 = iadd.i8 v2, v1
  return v3
}

; block0:
;   movzbq  0(%rdi), %rax
;   addl    %eax, %esi, %eax
;   ret

function %no_merge_if_lookback_use(i64, i64) -> i64 {
block0(v0: i64, v1: i64):
  v2 = load.i64 v0
  v3 = iadd.i64 v2, v0
  store.i64 v3, v1
  v4 = load.i64 v3
  return v4
}

; block0:
;   movq    0(%rdi), %r8
;   movq    %r8, %r9
;   addq    %r9, %rdi, %r9
;   movq    %r9, 0(%rsi)
;   movq    0(%r8,%rdi,1), %rax
;   ret

function %merge_scalar_to_vector(i64) -> i32x4 {
block0(v0: i64):
  v1 = load.i32 v0
  v2 = scalar_to_vector.i32x4 v1

  jump block1
block1:
  return v2
}

; block0:
;   movss   0(%rdi), %xmm0
;   jmp     label1
; block1:
;   ret

function %cmp_mem(i64) -> i64 {
block0(v0: i64):
  v1 = load.i64 v0
  v2 = icmp eq v0, v1
  v3 = uextend.i64 v2
  return v3
}

; block0:
;   cmpq    0(%rdi), %rdi
;   setz    %dl
;   movzbq  %dl, %rax
;   ret

